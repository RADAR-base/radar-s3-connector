apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext.website = 'https://radar-base.org'
ext.githubUrl = 'https://github.com/RADAR-base/RADAR-HDFS-Sink-Connector'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        RadarPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.description
                url = githubUrl
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'blootsvoets'
                        name = 'Joris Borgdorff'
                        email = 'joris@thehyve.nl'
                    }
                    developer {
                        id = 'nivemaham'
                        name = 'Nivethika'
                        email = 'nivethika@thehyve.nl'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = githubUrl + '/issues'
                }
                organization {
                    name = 'RADAR-base'
                    url = website
                }
                scm {
                    connection = 'scm:git:' + githubUrl
                    url = githubUrl
                }
            }
        }
    }
}



bintray {
    user System.getenv('BINTRAY_USER')? System.getenv('BINTRAY_USER') : project.property('bintrayUser')
    key System.getenv('BINTRAY_API_KEY') ? System.getenv('BINTRAY_API_KEY') : project.property('bintrayApiKey')
    override false
    publications 'RadarPublication'
    pkg {
        repo = project.group
        name = project.name
        userOrg = 'radar-base'
        licenses = ['Apache-2.0']
        vcsUrl = githubUrl
        version {
            name = project.version
            desc = 'RADAR-HDFS-Sink-Connector'
            vcsTag = System.getenv('TRAVIS_TAG')
        }
    }
}
